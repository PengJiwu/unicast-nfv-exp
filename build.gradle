import org.ajoberstar.grgit.Grgit

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'eclipse'

version = '0.1'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"


buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.ajoberstar:grgit:1.7.0'
  }
}

ext {
  git = Grgit.open(file('.'))
  revision = git.head().abbreviatedId
}


repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.12'
  compile 'org.jgrapht:jgrapht-core:0.9.2'
  compile 'org.jgrapht:jgrapht-ext:0.9.2'
  compile 'ch.qos.logback:logback-classic:1.1.7'
  compile 'com.google.guava:guava:19.0'
  compile 'org.jetbrains:annotations:15.0'
}

idea {
  project {
    languageLevel = '1.8'
  }
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

allprojects {
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked" << "-Xlint:all"
    }
  }
}

mainClassName = 'Simulation.Simulation'

task fatJar(type: Jar) {
  manifest {
    attributes 'Main-Class': mainClassName
  }
  baseName = "${project.name}-${revision}-all"
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

